
pkgname=mailx
pkgver=12.4
pkgrel=1
pkgdesc="A commandline utility for sending email"
arch=('x86_64')
url="http://heirloom.sourceforge.net/mailx.html"
license=('BSD')
groups=('base')
depends=('openssl' 'krb5')
optdepends=('smtp-forwarder: for sending mail')
backup=('etc/mail.rc')
source=("http://sourceforge.net/projects/heirloom/files/heirloom-mailx/$pkgver/mailx-$pkgver.tar.bz2"
        "http://www.linuxfromscratch.org/patches/blfs/svn/mailx-12.4-openssl_1.0.0_build_fix-1.patch")
md5sums=('0c93759e34200eb56a0e7c464680a54a'
         '7b7e904cfa99674aa4221fb231f5e94e')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i $srcdir/mailx-12.4-openssl_1.0.0_build_fix-1.patch

  # For Linux and BSD, this should be set.
  echo "set bsdcompat" >> nail.rc

  sed -i "s/pg/less/" cmd1.c

  echo PREFIX=/usr \
       MAILRC=/etc/nail.rc \
       SENDMAIL=/usr/sbin/sendmail \
       MAILSPOOL=/var/spool/mail \
       UCBINSTALL=/usr/bin/install > makeflags

  make `cat makeflags` IPv6=-DHAVE_IPv6_FUNCS
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" `cat makeflags` install

  # For compatibility with the old mailx program
  ln -sf mailx "${pkgdir}"/usr/bin/mail
  ln -sf mailx "${pkgdir}"/usr/bin/nail
  ln -sf mailx.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz

  install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
