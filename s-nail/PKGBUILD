
pkgname=s-nail
pkgver=14.8.8
pkgrel=1
pkgdesc="Mail processing system and commandline utility for sending email"
arch=('x86_64')
url="https://www.sdaoden.eu/code.html#s-naill"
license=('BSD')
groups=('base')
depends=('openssl' 'krb5')
optdepends=('postfix: for sending mail')
backup=('etc/mail.rc')
conflicts=('mailx')
replaces=('mailx')
provides=('mailx')
source=("http://www.sdaoden.eu/downloads/${pkgname}-${pkgver}.tar.xz")
md5sums=('53e353b4975ea7dcd161dd1423e3867a')

build() {
  cd ${pkgname}-${pkgver}

  make PREFIX=/usr \
       SYSCONFDIR=/etc \
       MAILSPOOL=/var/spool/mail \
       MANDIR=/usr/share/man \
       SID= NAIL=mail \
       WANT_AUTOCC=0 \
       LIBEXECDIR=/usr/lib \
       config

  make build
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} packager-install

  # For compatibility with the old mailx program
  ln -sf mail ${pkgdir}/usr/bin/mailx
  ln -sf mail ${pkgdir}/usr/bin/nail
  ln -sf mail.1.gz ${pkgdir}/usr/share/man/man1/mailx.1.gz

  install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
