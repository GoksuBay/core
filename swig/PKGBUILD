
pkgname=swig
pkgver=3.0.10
pkgrel=2
pkgdesc="A compiler that makes it easy to integrate C and C++ code with scripting languages"
arch=('x86_64')
url="http://www.swig.org/"
license=('custom')
depends=('pcre' 'zlib')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")  
md5sums=('bb4ab8047159469add7d00910e203124')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE-UNIVERSITIES ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}

