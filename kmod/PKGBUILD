
pkgname=kmod
pkgver=20
pkgrel=1
_kver=3.18.7-1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
checkdepends=('linux-headers')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        "depmod-search.conf")
md5sums=('d6f4fef718a50bd88080de6a43bc64d8'
         '9ae9acc68ca27134f55a0959073b8e53')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --with-zlib 
  make
}

check() {
  # Changes with 20 http://git.kernel.org/cgit/utils/kernel/kmod/kmod.git/tree/NEWS
  make -C "$pkgname-$pkgver" check KDIR="/lib/modules/$_kver/build/Makefile" KVER="$_kver"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d "$pkgdir/sbin"
  
  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}
