
pkgname=sudo
pkgver=1.8.14
_pkgver=1.8.14
pkgrel=2
pkgdesc="Give certain users the ability to run some commands as root."
arch=('x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam' 'libldap')
backup=('etc/sudoers' 'etc/pam.d/sudo')
install=sudo.install
options=('!libtool')
source=("ftp://ftp.sudo.ws/pub/sudo/${pkgname}-${_pkgver}.tar.gz"
        'sudo.pam')
md5sums=('76d12cb91865b5935a930478f4bc93bd'
         'a929068d0916daae0cf69bc8435c054e')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  ./configure --prefix=/usr \
              --with-pam \
              --libexecdir=/usr/lib \
              --with-ldap \
              --with-ldap-conf-file=/etc/openldap/ldap.conf \
              --with-rundir=/run/sudo \
              --with-env-editor \
              --with-all-insults \
              --with-logfac=auth \
              --enable-tmpfiles.d 

  make
}

check() {
  cd "$srcdir/$pkgname-${_pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # This is to ensure consistency with the permissions specified
  # in the 'filesystem' package.
  install -d -m755 "${pkgdir}/var/lib"

  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
  
  # changes with 1.8.10, added .install for this
  rmdir "$pkgdir/run/sudo"
  rmdir "$pkgdir/run"

  install -D -m644 doc/LICENSE "${pkgdir}/usr/share/licenses/sudo/LICENSE"
}

