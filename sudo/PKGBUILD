
pkgname=sudo
pkgver=1.8.10.2
_pkgver=1.8.10p2
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root."
arch=('x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam' 'libldap')
backup=('etc/sudoers' 'etc/pam.d/sudo')
install=sudo.install
options=('!libtool')
source=("ftp://ftp.sudo.ws/pub/sudo/${pkgname}-${_pkgver}.tar.gz"
        'sudo.pam'
        'sudo.tmpfiles.conf')
md5sums=('5e5eab1036a7cc2c088ab0d9b6b6a42e'
         'a929068d0916daae0cf69bc8435c054e'
         '5d124e73d02d9f0869b8ed5d182917b6')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  ./configure --prefix=/usr \
              --with-pam \
              --libexecdir=/usr/lib \
              --with-ldap \
              --with-ldap-conf-file=/etc/openldap/ldap.conf \
              --with-rundir=/run/sudo \
              --with-env-editor \
              --with-all-insults \
              --with-logfac=auth

  make
}

check() {
  cd "$srcdir/$pkgname-${_pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # This is to ensure consistency with the permissions specified
  # in the 'filesystem' package.
  install -d -m755 "${pkgdir}/var/lib"

  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
  
  # changes with 1.8.10, added .install for this
  rmdir "$pkgdir/run/sudo"
  rmdir "$pkgdir/run"

  install -Dm644 "$srcdir/sudo.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/sudo.conf"

  install -D -m644 doc/LICENSE "${pkgdir}/usr/share/licenses/sudo/LICENSE"
}

