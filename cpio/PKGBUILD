
pkgname=cpio
pkgver=2.11
pkgrel=5
pkgdesc="A tool to copy files into or out of a cpio or tar archive"
arch=('x86_64')
license=('GPL')
url="http://www.gnu.org/software/cpio"
depends=('glibc')
source=("ftp://ftp.gnu.org/gnu/cpio/cpio-${pkgver}.tar.gz")
install=cpio.install
md5sums=('1112bb6c45863468b5496ba128792f6c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix build issue with glibc-2.17
  sed -i -e '/gets is a/d' */stdio.in.h
  
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  
  rm -f ${pkgdir}/usr/bin/mt ${pkgdir}/pkg/usr/share/man/man1/mt.1
  mv ${pkgdir}/usr/bin ${pkgdir}/bin
  rm -rf ${pkgdir}/usr/libexec
  # remove mt manpage 
  rm $pkgdir/usr/share/man/man1/mt.1 
  # remove infodir
  rm $pkgdir/usr/share/info/dir
}

