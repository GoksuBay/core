
pkgname=pcmciautils
pkgver=018
pkgrel=6
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=('x86_64')
url="http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmcia.html"
license=('GPL')
groups=('base')
depends=('sysfsutils' 'systemd')
conflicts=('pcmcia-cs')
options=('!makeflags')
source=("http://ftp.de.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        'initcpio-install-pcmcia')
md5sums=('3c388cb559975b37a75cb5965e9800a9'
         '0dd0544b346d478f2bed35c1a91aa1dc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's,/usr/bin/install,install,g;
             s,/lib/udev,/usr/lib/udev,g' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install the mkinitpcio hook
  install -D -m644 ../initcpio-install-pcmcia ${pkgdir}/usr/lib/initcpio/install/pcmcia
}

