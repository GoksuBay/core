
pkgname=linux-firmware
pkgver=20131004
_b43=5.100.138
_legacy=3.130.20.0
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('x86_64')
url="http://git.kernel.org/?p=linux/kernel/git/dwmw2/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('b43-fwcutter' 'git')
conflicts=('linux-firmware-git'
           'ar9170-fw'
           'carl9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'b43-firmware'
           'b43-firmware-legacy'
           'amd-ucode')
replaces=('ar9170-fw'
           'carl9170'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'b43-firmware'
           'b43-firmware-legacy')
options=(!strip emptydirs)
source=("git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#branch=master"
        "http://mirror2.openwrt.org/sources/broadcom-wl-${_b43}.tar.bz2"
        "http://downloads.openwrt.org/sources/wl_apsta-${_legacy}.o")
md5sums=('SKIP'
         'f4e357b09eaf5d8b1f1920cf3493a555'
         'e08665c5c5b66beb9c3b2dd54aa80cb3')

# create tarball: source PKGBUILD && mksource

mksource() {
	  rm -vRf linux-firmware*
	  git clone git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git
	  pushd ${pkgname}
          git archive --format=tar --prefix linux-firmware-$(date +%Y%m%d)/ master | xz -9 > ../linux-firmware-$(date +%Y%m%d).tar.xz
	  popd
	  md5sum linux-firmware-$(date +%Y%m%d).tar.xz
}

package() {
  cd "${srcdir}/${pkgname}"

  install -d -m755 "${pkgdir}/usr/lib/firmware"
  cp -a * "${pkgdir}/usr/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/usr/lib/firmware/GPL*"
  mv "${pkgdir}/usr/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"

  # install b43-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/broadcom-wl-${_b43}/linux/wl_apsta.o

  # install b43-legacy-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/wl_apsta-${_legacy}.o
}
