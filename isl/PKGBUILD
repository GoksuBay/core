
pkgname=isl
pkgver=0.12
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('x86_64')
url="http://www.kotnet.org/~skimo/isl/"
depends=('gmp')
license=('MIT')
options=('!libtool')
source=("http://www.kotnet.org/~skimo/isl/${pkgname}-${pkgver}.tar.bz2")
md5sums=('eaa1968dda763f12dc84e45a892cd428')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}/" install
  
  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.2.0-gdb.py
  
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}

