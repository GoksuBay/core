
pkgname=net-tools
pkgver=1.60.3
_commit=adf6f5793fb74c723391a14efa8c2e4c7ef354f0
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="http://www.tazenda.demon.co.uk/phil/net-tools"
groups=('base')
depends=('glibc')
makedepends=('git')
source=("https://github.com/ecki/net-tools/archive/${_commit}.zip")
options=('!makeflags')
md5sums=('441f2b3b9ebaa3c28566e81d9f4b0033')

build() {
  cd ${srcdir}/${pkgname}-${_commit}
  
  yes "" | make
}

package() {
  cd ${srcdir}/${pkgname}-${_commit}
  make DESTDIR=${pkgdir} update

  # the following is provided by yp-tools
  rm "${pkgdir}"/bin/{nis,yp}domainname
  rm "${pkgdir}"/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm "${pkgdir}"/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}