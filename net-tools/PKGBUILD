
pkgname=net-tools
pkgver=1.60.5
_commit=115f1af2494ded1fcd21c8419d5e289bc4df380f
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="https://sourceforge.net/projects/net-tools/"
groups=('base')
depends=('glibc')
source=("http://sourceforge.net/code-snapshots/git/n/ne/net-tools/code.git/net-tools-code-${_commit}.zip")
options=('!makeflags')
md5sums=('243819dfa4505832f74bd436d3389148')

build() {
  cd ${pkgname}-code-${_commit}
  
  yes "" | make
}

package() {
  cd ${pkgname}-code-${_commit}
  make DESTDIR=${pkgdir} update

  # the following is provided by yp-tools
  rm ${pkgdir}/bin/{nis,yp}domainname
  rm ${pkgdir}/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm ${pkgdir}/bin/{hostname,dnsdomainname,domainname}
  rm -rf ${pkgdir}/usr/share/man/man1
}
