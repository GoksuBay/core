
pkgname=net-tools
pkgver=1.60.1
_pkgver=1.60.20120804git
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="http://www.tazenda.demon.co.uk/phil/net-tools"
groups=('base')
depends=('glibc')
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${_pkgver}.tar.xz"
	'Makefile.patch')
options=(!makeflags)
md5sums=('b43f354758cbd6ed139b0e62d9b8888f'
         'b1e0fd263b1f8302a58969a4c2782c42')
build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  sed -i -e '/Token/s/y$/n/' config.in
  patch -Np0 -i ${srcdir}/Makefile.patch
  yes "" | make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  make BASEDIR=${pkgdir} update

  # the following is provided by yp-tools
  rm "${pkgdir}"/bin/{nis,yp}domainname
  rm "${pkgdir}"/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm "${pkgdir}"/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}