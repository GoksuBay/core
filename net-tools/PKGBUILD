
pkgname=net-tools
pkgver=1.60.2
_pkgver=1.60.20140330git
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL')
url="http://www.tazenda.demon.co.uk/phil/net-tools"
groups=('base')
depends=('glibc')
makedepends=('git')
source=("net-tools::git://git.code.sf.net/p/net-tools/code#commit=68a4d784")
options=('!makeflags')
md5sums=('SKIP')

build() {
  cd ${srcdir}/${pkgname}
  
  yes "" | make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} update

  # the following is provided by yp-tools
  rm "${pkgdir}"/bin/{nis,yp}domainname
  rm "${pkgdir}"/usr/share/man/man1/{nis,yp}domainname.1

  # hostname is provided by inetutils
  rm "${pkgdir}"/bin/{hostname,dnsdomainname,domainname}
  rm -rf "${pkgdir}"/usr/share/man/man1
}