
pkgname=rp-pppoe
pkgver=3.12
pkgrel=2
pkgdesc="Roaring Penguin's Point-to-Point Protocol over Ethernet client"
arch=('x86_64')
url="http://www.roaringpenguin.com/pppoe/"
license=('GPL')
groups=('base')
depends=('ppp>=2.4.7' 'sh')
options=('!makeflags')
backup=(etc/ppp/{pppoe.conf,firewall-standalone,firewall-masq,pppoe-server-options})
source=("http://www.roaringpenguin.com/files/download/rp-pppoe-${pkgver}.tar.gz"
        'adsl.service'
        'kmode.patch')
md5sums=('216eb52b69062b92a64ee37fd71f4b66'
         'ae14bb45cdd6aad31395a301f96ddd82'
         '7f1bea3193b381bfaeb6cab1e0478300')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  patch -p2 -i ${srcdir}/kmode.patch
  
  ./configure --prefix=/usr \
              --enable-plugin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make PLUGIN_DIR="/usr/lib/rp-pppoe" DESTDIR="${pkgdir}" install
  
  install -Dm644 "${srcdir}/adsl.service" "${pkgdir}/usr/lib/systemd/system/adsl.service"
}
