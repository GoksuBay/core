
pkgname=libgcrypt
pkgver=1.8.3
pkgrel=2
pkgdesc="GNU's basic cryptographic library"
arch=('x86_64')
url="https://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error')
options=('!libtool' 'emptydirs')
source=("https://www.gnupg.org/ftp/gcrypt/libgcrypt/${pkgname}-${pkgver}.tar.bz2"
        "https://src.fedoraproject.org/rpms/libgcrypt/raw/master/f/libgcrypt-1.8.3-getrandom.patch")
sha1sums=('13bd2ce69e59ab538e959911dfae80ea309636e3'
          'a7a1cba01bc0686fe15ce67b03cb5aca9dae972b')

build() {
  cd ${pkgname}-${pkgver}
  # issues with slow boot on some systems since linux 4.16, use patch from Fedora to adjust getrandom
  # https://forum.kaosx.us/d/2169-slow-boot-hangs-a-long-time-before-gui-login/
  patch -p1 -i ${srcdir}/libgcrypt-1.8.3-getrandom.patch

  ./configure --prefix=/usr \
    --disable-padlock-support \
    --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
