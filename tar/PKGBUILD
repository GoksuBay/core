
pkgname=tar
pkgver=1.30
pkgrel=1
pkgdesc="Utility used to store, backup, and transport files"
arch=('x86_64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/tar/${pkgname}-${pkgver}.tar.xz")
sha1sums=('0d442c4565f8131745a5dff1cd08f7eaa797f679')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/tar \
    --bindir=/bin
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
