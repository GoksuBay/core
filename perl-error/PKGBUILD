
pkgname=perl-error
_realname=Error
pkgver=0.17019
pkgrel=2
pkgdesc="Perl/CPAN Error module - Error/exception handling in an OO-ish way"
url="http://search.cpan.org/dist/${_realname}/"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl>=5.14.2')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/${_realname}-${pkgver}.tar.gz")
md5sums=('47d4168c8dc064a73f6e49b6c81c21ab')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make 
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
