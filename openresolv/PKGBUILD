
pkgname=openresolv
pkgver=3.5.5
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=('x86_64')
url="http://roy.marples.name/projects/openresolv"
license=('custom:BSD')
depends=('bash')
makedepends=('systemd')
backup=('etc/resolvconf.conf')
provides=('resolvconf')
source=("http://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.bz2"
        'fs33801.patch')
md5sums=('31b07164b941d0ef0216b7d2a30bc573'
         '440790d3403b2ae2c4bb1d657c7e36a4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i $srcdir/fs33801.patch

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '2,25{s:^# \?::;p}' resolvconf.in > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

