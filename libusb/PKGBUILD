
pkgname=libusb
pkgver=1.0.19
pkgrel=1
pkgdesc="Library that provides generic access to USB device"
arch=('x86_64')
url="http://libusb.info"
license=('LGPL')
depends=('glibc' 'systemd')
options=('!libtool')
provides=('libusbx')
replaces=('libusbx')
conflicts=('libusbx')
source=("http://sourceforge.net/projects/libusb/files/libusb-1.0/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f9e2bb5879968467e5ca756cb4e1fa7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make check
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
