
pkgname=automake
pkgver=1.13.4
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('x86_64')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
install=automake.install
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('71b03c1bc4f587add12604587d9276d1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

check(){
  cd ${srcdir}/${pkgname}-${pkgver}
  # fails one test, bc of perl-5.18 
  make -k check || true
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

