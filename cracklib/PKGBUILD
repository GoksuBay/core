
pkgname=cracklib
pkgver=2.9.6
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="http://github.com/cracklib/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=("https://github.com/cracklib/cracklib/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c52f463585d85924b28cdc1e373ae06d')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --without-python
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 dicts/cracklib-small ${pkgdir}/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer ${pkgdir}/usr/share/cracklib/pw_dict
}

