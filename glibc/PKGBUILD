# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

pkgname=glibc
pkgver=2.20
pkgrel=1
pkgdesc="GNU C Library"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('linux-api-headers>=3.16' 'tzdata')
makedepends=('gcc>=4.8')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip')
install=glibc.install
source=("http://ftp.gnu.org/gnu/libc/${pkgname}-${pkgver}.tar.xz"
        'glibc-2.20-getifaddrs_internal-segfault.patch'
        'glibc-2.20-linux-3.16-additions.patch'
        'glibc-2.20-do_ftell_wide-memleak.patch'
        'locale.gen.txt'
        'locale-gen'
        'glibc.conf')
md5sums=('948a6e06419a01bd51e97206861595b0'
         '1c5d5c2017445c75dbc5c6d0c1e45ddb'
         '8f1059f431b842e54b12bde689620df8'
         'b50feeab78fa6ce0a8cfb41ee8dc1fd8'
         '07ac979b6ab5eeb778d55f041529d623'
         '476e9113489f93b348b21e144b6a8fcf'
         '8b3298de2392a10905a64ceccf56d2c1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/glibc-2.20-getifaddrs_internal-segfault.patch
  patch -p1 -i $srcdir/glibc-2.20-linux-3.16-additions.patch
  patch -p1 -i $srcdir/glibc-2.20-do_ftell_wide-memleak.patch
  
  cd ${srcdir}
  mkdir glibc-build
  cd glibc-build

  echo "slibdir=/lib" >> configparms
  
  # remove hardening options for building libraries
  CFLAGS=${CFLAGS/-fstack-protector/}
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  ${srcdir}/glibc-${pkgver}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --with-bugurl=https://github.com/KaOSx/core/issues \
      --enable-add-ons \
      --enable-obsolete-rpc \
      --enable-kernel=2.6.32 \
      --enable-bind-now --disable-profile \
      --enable-stackguard-randomization \
      --enable-lock-elision \
      --enable-multi-arch

  # build libraries with hardening disabled
  echo "build-programs=no" >> configparms
  make
  
  # re-enable hardening for programs
  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CC += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  make

  # remove harding in preparation to run test-suite
  sed -i '5,7d' configparms
}

check() {
  LDFLAGS=${LDFLAGS/--as-needed,/}

  cd ${srcdir}/glibc-build
  make check || true
}

package() {
  cd ${srcdir}/glibc-build

  install -dm755 ${pkgdir}/etc
  touch ${pkgdir}/etc/ld.so.conf

  make install_root=${pkgdir} install

  rm -f ${pkgdir}/etc/ld.so.{cache,conf}

  install -dm755 ${pkgdir}/{,usr/{sbin,lib/{,locale,systemd/system,tmpfiles.d}}}

  install -m644 ${srcdir}/glibc-${pkgver}/nscd/nscd.conf ${pkgdir}/etc/nscd.conf
  install -m644 ${srcdir}/glibc-${pkgver}/nscd/nscd.service ${pkgdir}/usr/lib/systemd/system
  install -m644 ${srcdir}/glibc-${pkgver}/nscd/nscd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/nscd.conf

  install -m755 ${srcdir}/locale-gen ${pkgdir}/usr/sbin
  install -m644 ${srcdir}/glibc-${pkgver}/posix/gai.conf ${pkgdir}/etc/gai.conf

  # create /etc/locale.gen
  install -m644 ${srcdir}/locale.gen.txt ${pkgdir}/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    ${srcdir}/glibc-${pkgver}/localedata/SUPPORTED >> ${pkgdir}/etc/locale.gen

  # fix for the linker
  sed -i '/RTLDLIST/s%lib64%lib%' ${pkgdir}/usr/bin/ldd
  # Comply with multilib binaries, they look for the linker in /lib64
  #mkdir ${pkgdir}/lib64
  cd ${pkgdir}/lib64
  ln -v -s ../lib/ld-2* .

  cd ${pkgdir}
  strip $STRIP_BINARIES sbin/{ldconfig,sln} \
                        usr/bin/{gencat,getconf,getent,iconv,locale} \
                        usr/bin/{localedef,pcprofiledump,rpcgen,sprof} \
                        usr/lib/getconf/* \
                        usr/sbin/{iconvconfig,nscd}

  strip $STRIP_STATIC usr/lib/*.a 

  strip $STRIP_SHARED lib/{libanl,libBrokenLocale,libcidn,libcrypt}-*.so \
                      lib/libnss_{compat,dns,files,hesiod,nis,nisplus}-*.so \
                      lib/{libdl,libm,libnsl,libresolv,librt,libutil}-*.so \
                      lib/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib/{audit,gconv}/*.so

  # Add /usr/lib32 to the default library search path
  install -Dm644 "$srcdir/glibc.conf" "$pkgdir/etc/ld.so.conf.d/glibc.conf"
}
