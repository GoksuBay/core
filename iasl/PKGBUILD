
pkgname=iasl
pkgver=20130418
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('53936808b460cb7963dc8e7f9e02a3bb'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix
  sed -i -e 's/_CYGWIN/_LINUX/g' -e 's/-Werror//g' Makefile.config

  make BITS=64
}

package() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix

  make DESTDIR=${pkgdir} install
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
