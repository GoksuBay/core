
pkgname=iasl
pkgver=20150204
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('18f0762e624e60bfc847ba359b3c5ff9'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix
  sed -i -e 's/_CYGWIN/_LINUX/g' -e 's/-Werror//g' Makefile.config

  make clean
  make
}

package() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix

  make DESTDIR=${pkgdir} install
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
