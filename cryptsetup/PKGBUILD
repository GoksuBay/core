
pkgname=cryptsetup
pkgver=1.6.1
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=('x86_64')
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
options=('!libtool' '!emptydirs')
source=("http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2"
        "http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2.asc"
        'encrypt_hook'
	'encrypt_install')
md5sums=('f374d11e3b0e7ca0f805756fd02e34ff'
         'SKIP'
         'c279d86d6dc18322c054d2272ebb9e90'
         'abe9bb42341e8f2ecd2b07e4244ac066')
build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
}

