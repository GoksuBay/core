
pkgname=flex
pkgver=2.6.3
pkgrel=2
pkgdesc="A tool for generating text-scanning programs"
arch=('x86_64')
url="https://github.com/westes/flex"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
makedepends=('help2man')
source=("https://github.com/westes/flex/releases/download/v${pkgver}/flex-${pkgver}.tar.gz"
        "https://github.com/westes/flex/commit/f5d87f1a26f4a5c3402497008ae10e9a1345d327.diff")
md5sums=('a5f65570cd9107ec8a8ec88f17b31bb1'
         '7d6a72fad1b12922996964d80318ac3a')

prepare() {
  cd ${pkgname}-${pkgver}
  # build issues with 2.6.3 https://github.com/westes/flex/issues/162
  patch -p1 -i ${srcdir}/f5d87f1a26f4a5c3402497008ae10e9a1345d327.diff
}

build() {
  cd ${pkgname}-${pkgver} 
  
  ./configure --prefix=/usr 
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  ln -s flex ${pkgdir}/usr/bin/lex 
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt 
}
