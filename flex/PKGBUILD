
pkgname=flex
pkgver=2.6.2
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('x86_64')
url="https://github.com/westes/flex"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
makedepends=('help2man')
source=("https://github.com/westes/flex/releases/download/v${pkgver}/flex-${pkgver}.tar.gz")
md5sums=('cc6d76c333db7653d5caf423a3335239')

build() {
  cd ${pkgname}-${pkgver} 
  
  ./configure --prefix=/usr 
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  ln -s flex ${pkgdir}/usr/bin/lex 
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt 
}
