
pkgname=flex
pkgver=2.6.3
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('x86_64')
url="https://github.com/westes/flex"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
makedepends=('help2man')
source=("https://github.com/westes/flex/releases/download/v${pkgver}/flex-${pkgver}.tar.gz")
md5sums=('a5f65570cd9107ec8a8ec88f17b31bb1')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix build issues with 2.6.2 https://github.com/westes/flex/issues/113
  #patch -p1 -i ${srcdir}/347652c32b4614995acd4ee0d686499da2070d9e.diff
}

build() {
  cd ${pkgname}-${pkgver} 
  
  ./configure --prefix=/usr 
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  ln -s flex ${pkgdir}/usr/bin/lex 
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt 
}
