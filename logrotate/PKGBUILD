
pkgname=logrotate
pkgver=3.8.5
pkgrel=1
pkgdesc="Rotates system logs automatically"
url='https://fedorahosted.org/logrotate/'
arch=('x86_64')
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip' 'acl')
backup=('etc/logrotate.conf')
source=("https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz"
        'logrotate.conf' 
        'logrotate.cron.daily')
md5sums=('d3c13e2a963a55c584cfaa83e96b173d'
         '1bcb4769f347c7de346f6264e57f48d8'
         'aa8ac8283908b6114483a293adcb650f')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  make WITH_ACL=yes 
}

check() {
	cd "$srcdir/${pkgname}-${pkgver}"
	
	make test
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make PREFIX=$pkgdir install
  
  install -dm755 $pkgdir/etc/logrotate.d
  install -Dm644 $srcdir/logrotate.conf $pkgdir/etc/logrotate.conf
  install -Dm744 $srcdir/logrotate.cron.daily $pkgdir/etc/cron.daily/logrotate
}
