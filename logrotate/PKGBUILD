
pkgname=logrotate
pkgver=3.8.9
pkgrel=1
pkgdesc="Rotates system logs automatically"
url='https://fedorahosted.org/logrotate/'
arch=('x86_64')
license=('GPL')
groups=('base')
depends=('popt' 'gzip' 'acl')
backup=('etc/logrotate.conf')
source=("https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz"
        'logrotate.conf' 
        'logrotate.service'
        'logrotate.timer'
        'path.patch')
md5sums=('2660f30742da79870d15d042b07829f6'
         '416de14f92ef4361adad8b6e7065245a'
         '57039a96a716391fad46c7eb1bf47b52'
         '287c2ad9b074cb5478db7692f385827c'
         'e76526bcd6fc33c9d921e1cb1eff1ffb')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p0 -i "$srcdir/path.patch"
  
  ./autogen.sh
  ./configure --prefix=/usr \
       --mandir=/usr/share/man \
       --with-acl
  
  make 
}

check() {
	cd "$srcdir/${pkgname}-${pkgver}"
	
	make test
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install
  
  install -dm755 $pkgdir/etc/logrotate.d
  install -Dm644 $srcdir/logrotate.conf $pkgdir/etc/logrotate.conf
  
  install -D -m644 ${srcdir}/logrotate.timer ${pkgdir}/usr/lib/systemd/system/logrotate.timer
  install -D -m644 ${srcdir}/logrotate.service ${pkgdir}/usr/lib/systemd/system/logrotate.service
  install -d -m755 ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
  ln -s ../logrotate.timer ${pkgdir}//usr/lib/systemd/system/multi-user.target.wants/logrotate.timer
}
