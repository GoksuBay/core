
pkgname=xfsprogs
pkgver=4.16.0
_pkgver=4.16.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="http://xfs.org/index.php/Main_Page"
groups=('base')
depends=('util-linux')
options=('!makeflags' '!libtool')
source=("https://www.kernel.org/pub/linux/utils/fs/xfs/xfsprogs/${pkgname}-${_pkgver}.tar.xz")
sha256sums=('4dedd5021eab0dcf77c2257642523546fce67fd0b017f8cf3bbe6678302e3300')

build() {
  cd ${pkgname}-${_pkgver}
  
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  make DIST_ROOT=${pkgdir} install install-dev
  
  chown -R root ${pkgdir}
  chgrp -R root ${pkgdir}
  mv ${pkgdir}/lib/libhandle.so* ${pkgdir}/usr/lib/
  rm -rf ${pkgdir}/lib
}

