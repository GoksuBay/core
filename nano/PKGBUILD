
pkgname=nano
pkgver=2.8.5
_pkgver=2.8
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="https://www.nano-editor.org"
groups=('base')
depends=('glibc' 'ncurses' 'sh')
makedepends=('groff')
source=("https://www.nano-editor.org/dist/v${_pkgver}/nano-${pkgver}.tar.gz")
backup=(etc/nanorc)
md5sums=('e5a090e890647a016d05ef8f9fbb0187')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-color \
        --enable-nanorc \
        --enable-multibuffer \
        --disable-wrapping-as-root
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}
