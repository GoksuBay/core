
pkgname=mdadm
pkgver=3.3
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=('x86_64')
license=('GPL')
url="http://www.cse.unsw.edu.au/~neilb/source/mdadm/"
groups=('base')
conflicts=('mkinitcpio<0.7')
depends=('glibc')
backup=('etc/mdadm.conf')
source=("ftp://ftp.kernel.org/pub/linux/utils/raid/mdadm/mdadm-$pkgver.tar.bz2"
        'mdadm.conf' 
        'mdadm_install'
        'mdadm_hook'
        'mdadm_udev_install'
        'mdadm.service'
        'disable-werror.patch')
md5sums=('8ac04259cdd74b4566c3b6dea9414b57'
         '5a37c112aa07dccdde62f9fa5b888607'
         '6e1a3377e2ac33e9fb222ab38050c654'
         '8e02ee595dfa4e5178ed8886e822cf3a'
         '7b052d051565d8128b09ca6f673d1b08'
         'aafb5f9ac8437a284cbf948b9b13b179'
         '599745ed2bec4489e83991cff89c46ee')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 -i ../disable-werror.patch
  
  make CXFLAGS="$CFLAGS"
  # build static mdassemble for initramfs
  make MDASSEMBLE_AUTO=1 mdassemble

}

package() {
  cd $srcdir/$pkgname-$pkgver
  make INSTALL=/usr/bin/install DESTDIR=$pkgdir install
  install -D -m755 mdassemble $pkgdir/sbin/mdassemble
  install -D -m644 ../mdadm.conf $pkgdir/etc/mdadm.conf
  install -D -m644 ../mdadm_install $pkgdir/usr/lib/initcpio/install/mdadm
  install -D -m644 ../mdadm_hook $pkgdir/usr/lib/initcpio/hooks/mdadm
  install -D -m644 ../mdadm_udev_install $pkgdir/usr/lib/initcpio/install/mdadm_udev
  
  # systemd service file
  install -D -m644 $srcdir/mdadm.service $pkgdir/usr/lib/systemd/system/mdadm.service
}
