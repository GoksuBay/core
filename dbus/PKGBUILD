
pkgname=dbus
pkgver=1.10.2
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=('x86_64')
license=('GPL' 'custom')
depends=('expat' 'coreutils' 'filesystem' 'shadow') 
makedepends=('libx11' 'systemd' 'xmlto')
conficts=('dbus-core')
replaces=('dbus-core')
provides=('dbus-core')
options=('!libtool')
source=("http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz")
md5sums=('2428919cc77b8d0028d65ee4d5dbef31')

build() {
  cd dbus-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd
  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/var/run"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}
