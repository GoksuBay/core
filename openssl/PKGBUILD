
pkgname=openssl
pkgver=1.0.2.12
_pkgver=1.0.2l
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl' 'zlib')
optdepends=('ca-certificates')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_pkgver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch'
        'bctest'
        'pod2mantest')
md5sums=('f85123cd390e864dfbe517e7616e6566'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         'ca85ae891de94faa821c2c6f4dba7f5a'
         '0811d285202cebe91003771475802b4f')

build() {
	cd ${pkgname}-${_pkgver}
	# add missing files to run bc test and install commands, 1.0.2e tar issue
	#cp $srcdir/bctest test
	#cp $srcdir/pod2mantest util
	
	openssltarget='linux-x86_64'
	optflags='enable-ec_nistp_64_gcc_128'

	patch -p0 -i $srcdir/no-rpath.patch
	# set ca dir to /etc/ssl by default
	patch -p0 -i $srcdir/ca-dir.patch
	
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
		shared zlib ${optflags} \
		${openssltarget} \
		-Wa,--noexecstack ${CFLAGS} ${LDFLAGS} \

	make depend
	make
}

check() {
	cd ${pkgname}-${_pkgver}
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i ${srcdir}/ca-dir.patch
	make test
	patch -p0 -i ${srcdir}/ca-dir.patch
}

package() {
	cd ${pkgname}-${_pkgver}
	make INSTALL_PREFIX=${pkgdir} MANDIR=/usr/share/man MANSUFFIX=ssl install
	
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
