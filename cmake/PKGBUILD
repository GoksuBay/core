
pkgname=cmake
pkgver=3.1.1
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="http://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info')
makedepends=('qt5-base')
install=${pkgname}.install
source=("http://www.cmake.org/files/v3.1/${pkgname}-${pkgver}.tar.gz")
md5sums=('0ae4f25dbec66697c0c3cec1b9e885ae')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
