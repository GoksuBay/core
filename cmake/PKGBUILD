
pkgname=cmake
pkgver=3.13.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="https://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info' 'libuv')
makedepends=('qt5-base')
#source=("https://www.cmake.org/files/v3.13/${pkgname}-${pkgver}.tar.gz")
source=("https://github.com/Kitware/CMake/releases/download/v3.13.2/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c925e7d2c5ba511a69f43543ed7b4182a7d446c274c7480d0e42cd933076ae25')

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --no-system-jsoncpp \
    --no-system-librhash \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
