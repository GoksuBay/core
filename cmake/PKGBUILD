
pkgname=cmake
pkgver=3.8.2
pkgrel=2
pkgdesc="A cross-platform open-source make system"
arch=('x86_64')
license=('custom')
url="https://www.cmake.org"
depends=('curl' 'libarchive' 'shared-mime-info' 'libuv')
makedepends=('qt5-base')
source=("https://www.cmake.org/files/v3.8/${pkgname}-${pkgver}.tar.gz")
md5sums=('b5dff61f6a7f1305271ab3f6ae261419')

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --system-libs \
    --qt-gui \
    --no-system-jsoncpp \
    --no-system-librhash \
    --parallel=$(/usr/bin/getconf _NPROCESSORS_ONLN)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 Copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
