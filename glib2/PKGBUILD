

pkgbase=glib2
pkgname=('glib2' 'glib2-docs')
pkgver=2.48.1
_pkgver=2.48
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=('x86_64')
makedepends=('pkg-config' 'python3' 'libxslt' 'docbook-xml' 'pcre' 'libffi' 'elfutils')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${_pkgver}/glib-${pkgver}.tar.xz"
        'revert-warn-glib-compile-schemas.patch'
        'gio-querymodules.hook'
        'glib-compile-schemes.hook')
md5sums=('67bd3b75c9f6d5587b457dc01cdcd5bb'
         '3265d1afbcf1f5f308e26c3f9f6ca899'
         'a52ea506b5cf162a4f72d09cc33fae50'
         '9e25d7de10bc4dc105f171e9ae46ef69')

prepare() {
  cd glib-${pkgver}
  
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
}
  
build() {
  cd glib-${pkgver}
  
  PYTHON=/usr/bin/python3 ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package_glib2() {
  depends=('pcre' 'libffi')
  options=('!docs' '!libtool' '!emptydirs')
  license=('LGPL')

  cd glib-${pkgver}
  make completiondir=/usr/share/bash-completion/completions DESTDIR=${pkgdir} install

  for _i in ${pkgdir}/usr/share/bash-completion/completions/*; do
      chmod -x "$_i"
  done

  rm -rf ${pkgdir}/usr/share/gdb/
  
  install -dm755 ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../glib-compile-schemes.hook ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ../gio-querymodules.hook ${pkgdir}/usr/share/libalpm/hooks/
}

package_glib2-docs() {
  pkgdesc="Documentation for glib2"
  license=('custom')
  options=('docs' '!emptydirs')
  
  cd glib-${pkgver}/docs
  make DESTDIR=${pkgdir} install
  install -m755 -d ${pkgdir}/usr/share/licenses/glib2-docs
  install -m644 reference/COPYING ${pkgdir}/usr/share/licenses/glib2-docs/

  rm -rf ${pkgdir}/usr/share/man
}
