# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.24
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.20' 'zlib')
checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
source=("ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.gz"
        'binutils-2.24-lto-testsuite.patch'
        'binutils-2.24-gold-testsuite-plugin.patch'
        'binutils-2.24-shared-pie.patch')
md5sums=('a5dd5dd2d212a282cc1d4a84633e0d88'
         'e68a1f221545f082baa5bd417cd077af'
         'e46bbb03abaa5db58c12247644dc4857'
         'ec59461c4febab9cfe1c1ef33326bc50')

prepare() {
  cd ${srcdir}/binutils-${pkgver}
  
  # libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
  
  patch -p1 -i $srcdir/binutils-2.24-lto-testsuite.patch
  patch -p1 -i $srcdir/binutils-2.24-gold-testsuite-plugin.patch
  patch -p1 -i $srcdir/binutils-2.24-shared-pie.patch

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://github.com/KaOSx/core/issues \
    --enable-ld=default --enable-gold --with-pic \
    --enable-plugins --enable-threads --enable-shared \
    --disable-werror --disable-multilib 

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=${pkgdir}/usr
}

check() {
  cd ${srcdir}/binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install

  # Remove Windows/Novell specific man pages
  rm ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*
  rm ${pkgdir}/usr/share/info/{configure,standards}.info

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
}
