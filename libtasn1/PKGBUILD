
pkgname=libtasn1
pkgver=4.10
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
depends=('glibc' 'texinfo')
checkdepends=('valgrind')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz")
md5sums=('f4faffdf63969d0e4e6df43b9679e8e5')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
      --with-packager=KaOS \
      --with-packager-bug-reports="https://github.com/KaOSx/core/issues" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
