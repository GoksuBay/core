
pkgname=dhcpcd
pkgver=6.11.3
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'sh')
makedepends=('ntp')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  
source=("http://roy.marples.name/downloads/dhcpcd/$pkgname-$pkgver.tar.xz" 
        'dhcpcd@.service'
        'dhcpcd.service')
md5sums=('d4f2f3ed4964197dee7767219c33a9df'
         '3b47ff473ee7f7b03062818b94fd3ed5'
         'c10ec3816b13fd5d59f696f7154f445a')

build() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|#include "queue.h"|#include "compat/queue.h"|' eloop.c

  ./configure --libexecdir=/usr/lib/dhcpcd \
       --dbdir=/var/lib/dhcpcd \
       --rundir=/run \
       --without-dev
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Create Binary Symlink
  install -d ${pkgdir}/usr/sbin
  ln -sf /sbin/dhcpcd ${pkgdir}/usr/sbin/dhcpcd

  # Install License
  install -d ${pkgdir}/usr/share/licenses/$pkgname
  sed 26q ${srcdir}/$pkgname-$pkgver/control.h \
        >> ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf # Disable ip4vall

  # install systemd unit
  install -Dm644 ${srcdir}/dhcpcd@.service ${pkgdir}/usr/lib/systemd/system/dhcpcd@.service
  install -Dm644 ${srcdir}/dhcpcd.service ${pkgdir}/usr/lib/systemd/system/dhcpcd.service
}
