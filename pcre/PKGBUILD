
pkgname=pcre
pkgver=8.36
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs' 'readline' 'zlib' 'bzip2' 'libedit')
options=('!libtool')
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b767bc9af0c20bc9c1fe403b0d41ad97')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --enable-utf \
        --enable-unicode-properties \
        --enable-jit \
        --enable-pcre16 \
        --enable-pcre32 \
        --enable-pcregrep-libz \
        --enable-pcregrep-libbz2 \
        --enable-pcretest-libreadline
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver} 
  
  make -j1 check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}