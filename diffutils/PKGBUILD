
pkgname=diffutils
pkgver=3.5
pkgrel=1
pkgdesc="Utility programs used for creating patch files"
arch=('x86_64')
url="http://www.gnu.org/software/diffutils"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
source=("ftp://ftp.gnu.org/gnu/diffutils/${pkgname}-${pkgver}.tar.xz")
md5sums=('569354697ff1cfc9a9de3781361015fa')

build() {
  cd ${pkgname}-${pkgver}
  # copyright test fails, known upstream, fixed for next release, disable this 1 test
  #echo 'exit 0' > gnulib-tests/test-update-copyright.sh || die
  
  ./configure --prefix=/usr \
    --with-packager="KaOS" \
    --with-packager-bug-reports="https://github.com/KaOSx/core/issues"
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

