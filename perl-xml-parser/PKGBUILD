
pkgname=perl-xml-parser
_realname=XML-Parser
pkgver=2.44
pkgrel=2
pkgdesc="Expat-based XML parser module for perl"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
conflicts=('perlxml')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_realname}-${pkgver}.tar.gz")
md5sums=('af4813fe3952362451201ced6fbce379')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  make install DESTDIR="${pkgdir}"
}
