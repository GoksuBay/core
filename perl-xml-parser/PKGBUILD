
pkgname=perl-xml-parser
_pkgname=XML-Parser
pkgver=2.44.01
_pkgver=2.44_01
pkgrel=1
pkgdesc="Expat-based XML parser module for perl"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_pkgname}/"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
conflicts=('perlxml')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('223f524d4ab3896e5149ec656f8561cd')

build() {
  cd ${_pkgname}-${_pkgver}

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${_pkgver}
  
  make test
}

package() {
  cd ${_pkgname}-${_pkgver}
  
  make install DESTDIR=${pkgdir}
}
