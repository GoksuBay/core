
pkgname=procps-ng
pkgver=3.3.8
pkgrel=2
pkgdesc='Utilities for monitoring your system and its processes'
arch=('x86_64')
url="http://sourceforge.net/projects/procps-ng/"
license=('GPL' 'LGPL')
groups=('base')
depends=('ncurses')
provides=('procps')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz")
md5sums=('aecbeeda2ab308f8d09dddcb4cb9a572')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --exec-prefix=/ --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install

  # provided by util-linux
  rm "${pkgdir}/bin/kill"
  rm "${pkgdir}/usr/share/man/man1/kill.1"
}
