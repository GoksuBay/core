
pkgname=netctl
pkgver=1.3
pkgrel=1
pkgdesc="Profile based systemd network management"
url="http://projects.archlinux.org/netctl.git/"
license=("GPL")
arch=('x86_64')
depends=('coreutils' 'iproute2' 'openresolv')
makedepends=('pkg-config') 
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for pppoe connections')
conflicts=('netcfg')
replaces=('netcfg')
provides=('netcfg')
source=("ftp://ftp.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz")
md5sums=('28fa105f2bcf2edaa849918445a28216')

package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}