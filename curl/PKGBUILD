
pkgname=curl
pkgver=7.38.0
pkgrel=2
pkgdesc="An URL retrival utility and library"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash' 'ca-certificates' 'libssh2')
options=('!libtool')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"
        'curlbuild.h'
        '0001-threaded-resolver-revert-Curl_expire_latest-switch.patch')
md5sums=('af6b3c299bd891f43cb5f76c4091b7b4'
         '751bd433ede935c8fae727377625a8ae'
         'deb28ba80ade06c07b67d392d133afb5')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # https://sourceforge.net/p/curl/bugs/1426/?page=1 xbmc stream fail
    patch -p1 -i ${srcdir}/0001-threaded-resolver-revert-Curl_expire_latest-switch.patch

    ./configure \
        --with-random=/dev/urandom \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-ipv6 \
        --disable-ldaps \
        --disable-ldap \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --without-libidn \
        --enable-threaded-resolver
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    install -Dm644 docs/libcurl/libcurl.m4 ${pkgdir}/usr/share/aclocal/libcurl.m4
    
    mv "${pkgdir}/usr/include/curl/curlbuild.h" "${pkgdir}/usr/include/curl/curlbuild-64.h"
    install -m 644 ${srcdir}/curlbuild.h "${pkgdir}/usr/include/curl/curlbuild.h"
}
