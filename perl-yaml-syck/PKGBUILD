
pkgname=perl-yaml-syck
_realname=YAML-Syck
pkgver=1.25
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
license=('custom')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TO/TODDR/${_realname}-$pkgver.tar.gz")
md5sums=('847f315cbd074b42c44f360383ac13e9')

build() {
  cd "${srcdir}/${_realname}-$pkgver"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-$pkgver"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete

  # license.
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
