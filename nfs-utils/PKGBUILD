
pkgname=nfs-utils
pkgver=1.2.9
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,idmapd.conf,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc' 'librpcsecgss' 'nfsidmap' 'libevent'
         'libgssglue' 'device-mapper' 'sqlite')
makedepends=('pkgconfig' 'autoconf' 'automake')
install=nfs-utils.install
source=("http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
	'nfs-common.conf'
	'nfs-server.conf'
	'exports'
	'idmapd.conf'
        'nfs'
	'nfs-utils-1.1.4-mtab-sym.patch'
	'nfs-utils-1.1.4-no-exec.patch'
	'rpc-gssd.service'
	'rpc-mountd.service'
	'rpc-svcgssd.service'
	'rpc-idmapd.service'
	'rpc-statd.service'
	'nfsd.service'
	'var-lib-nfs-rpc_pipefs.mount'
	'proc-fs-nfsd.mount'
	'blkmapd.service'
	'nfs-utils.conf'
        'id_resolver.conf')
md5sums=('18869d16db3f49c053f8c68eba3fe2e0'
         'f73f197a16b02c3e248488ec35c4cf43'
         '9cef69bc686cc5dcac23fbb51450747d'
         'ff585faf410a62c4333a027c50b56bae'
         'eb4f4027fab6fc1201f1ca04f5954c76'
         'f5e7bba09a46c5c5d7007cac6eff9df5'
         '7674106eaaa4c149bccd4f05fe3604e9'
         '4f4827dfc93008dfadd0a530ad0872b2'
         '3694619813cebc5b4502b58f1992b442'
         '965311784d49a7d126d512cadbe91deb'
         'e05705d9ccccaeaeb1ecaee20adc05bc'
         '359d84a85f8781dc3f45f4cf1dbe4b53'
         '60d82e2d2d19d21ceb72665e54bf913e'
         '5d33d2e754fd37280365b287603bac90'
         '1cd65909fa0983047f3f06a3ab352401'
         '8f1b5282795895c9b8ce8430d20cdda6'
         '8ffc2ebe932d29efe17d6f3f23d5b975'
         '8ac484023d786766d287ccbe878ae4ba'
         'af3d06241699546ca7bb8f045b0ec95e')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -p1 -i ../nfs-utils-1.1.4-mtab-sym.patch
  
  ./configure --prefix=/usr --enable-nfsv4 --enable-nfsv41 --enable-gss \
              --without-tcp-wrappers --with-statedir=/var/lib/nfs \
              --enable-ipv6 --sysconfdir=/etc --enable-libmount-mount \
              --with-gssglue \
              --enable-mountconfig 
  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install

  sed -i '1s/python$/python2/' "$pkgdir"/usr/sbin/{nfsiostat,mountstats}

  # Configuration
  install -D -m 644 ../exports "$pkgdir/"etc/exports
  install -D -m 644 ../idmapd.conf "$pkgdir/"etc/idmapd.conf
  install -D -m 644 ../nfs-common.conf "$pkgdir/"etc/conf.d/nfs-common.conf
  install -D -m 644 ../nfs-server.conf "$pkgdir/"etc/conf.d/nfs-server.conf
  install -D -m 644 ../nfs "$pkgdir/"etc/conf.d/nfs
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir/"etc/nfsmount.conf

  install -D -m 644 ../id_resolver.conf "$pkgdir/"etc/request-key.d/id_resolver.conf
  # systemd files
  for i in ${srcdir}/*.{service,mount}; do
    install -D -m 644 $i "$pkgdir/usr/lib/systemd/system/${i##*/}"
  done
  install -D -m 644 ../nfs-utils.conf "$pkgdir/"usr/lib/modules-load.d/nfs-utils.conf
  # directories
  mkdir "$pkgdir/"etc/exports.d
  mkdir -m 555 "$pkgdir/"var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir/"var/lib/nfs/v4recovery
}

