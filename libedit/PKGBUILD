
pkgname=libedit
pkgver=20160903_3.1
_pkgver=20160903-3.1
_buildver=20160903-3.1
pkgrel=3
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
arch=('x86_64')
url='http://www.thrysoee.dk/editline/'
license=('BSD')
depends=('ncurses')
options=('!libtool')
source=("http://www.thrysoee.dk/editline/libedit-${_pkgver}.tar.gz")
md5sums=('0467d27684c453a351fbcefebbcb16a3')

build() {
	cd ${pkgname}-${_buildver}
	
	./configure --prefix=/usr --enable-widec --enable-static=no
	make
}

package() {
	cd ${pkgname}-${_buildver}
	make prefix=${pkgdir}/usr install

	cp ${pkgdir}/usr/share/man/man3/editline.3 ${pkgdir}/usr/share/man/man3/el.3
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/libedit/LICENSE
	# readline conflict
	rm ${pkgdir}/usr/share/man/man3/history.3
}
