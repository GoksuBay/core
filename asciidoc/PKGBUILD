 
pkgname=asciidoc
pkgver=8.6.9
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('x86_64')
url='http://www.methods.co.nz/asciidoc/'
license=('GPL')
depends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('lilypond: music-filter'
            'imagemagick: music-filter (used in conjunction with lilypond)'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'lynx: text generation'
            'w3m: text generation (alternative to lynx)')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c59018f105be8d022714b826b0be130a')

build() {
  cd ${pkgname}-${pkgver}

  # python2 fix
  for file in asciidocapi.py a2x.py asciidoc.py filters/music/music2png.py filters/latex/latex2png.py \
      filters/code/code-filter.py filters/graphviz/graphviz2png.py; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}

  install -Dm644 asciidocapi.py \
    ${pkgdir}/usr/lib/python2.7/site-packages/asciidocapi.py

  install -Dm644 vim/syntax/asciidoc.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/asciidoc.vim
}