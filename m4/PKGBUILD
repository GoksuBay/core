
pkgname=m4
pkgver=1.4.16
pkgrel=3
pkgdesc="m4 macro processor"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/m4"
install=m4.install
groups=('base-devel')
depends=('glibc' 'bash')
source=("ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz"
        'm4-1.4.16-readlink-einval.patch')
md5sums=('7548ec061a1ba993790159764f522d0e'
         'cea138fa9b568d06e46269611cec8dd0')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  # for latest glibc
  sed -i -e '/gets is a/d' lib/stdio.in.h
  
  # patch testsuite failure with newer kernels
  patch -p1 -i $srcdir/m4-1.4.16-readlink-einval.patch
  
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  
  make prefix=${pkgdir}/usr install
}
