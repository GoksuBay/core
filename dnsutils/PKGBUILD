
pkgname=dnsutils
# _pkver when P version used.
pkgver=9.9.5
_pkgver=9.9.5
pkgrel=1
pkgdesc="Various DNS utilities - dig host nslookup nsupdate"
arch=('x86_64')
url="https://www.isc.org/software/bind"
license=('custom:ISC')
depends=('openssl')
options=('makeflags')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
	'tools-only.patch')
md5sums=('e676c65cad5234617ee22f48e328c24e'
         '0f2865c239cd247e5ed45b09f90575f7')

build() {
	cd "${srcdir}/bind-${_pkgver}"

	#patch -p1 -i "${srcdir}/tools-only.patch"
	export STD_CDEFINES='-DDIG_SIGCHASE'

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl  

	make
}

package() {
	cd "${srcdir}/bind-${_pkgver}"
	
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd bin; make DESTDIR="${pkgdir}" install
}
